#captcha
# HTML._CAPTCHA_IMG_WRAPPER = <img src="data:image/png;base64,%bind(:1)" alt="" style="float:left;"/>

Component Record &captchaRec;
Component string &captchaCode;
Component string &captchaVerified;
Component number &refreshCount;

Function clearCaptcha()
   
   &captchaRec.HTMLAREA1.Value = "";
   &captchaRec.HTMLAREA2.Value = GetHTMLText(HTML.UA_CAPTCHA_CLEAR_SCRIPT);
   &captchaRec.AUTH_KEYWORD.Visible = False;
   
   &captchaCode = "";
   &captchaVerified = "";
   
End-Function;

Function buildCaptcha() Returns string
   
   If None(&captchaRec) Then
      Local string &captchaRecName;
      SQLExec("SELECT RECNAME FROM PSPNLFIELD WHERE PNLNAME=:1 AND SUBPNLNAME='UA_GET_CAPTCHA_SBP'", %Page, &captchaRecName);
      &captchaRec = GetRecord(@("RECORD." | &captchaRecName));
   End-If;
   
   &refreshCount = &refreshCount + 1;
   
   clearCaptcha();
   
   If &refreshCount > 4 Then
      &refreshCount = 0;
      Return "";
   End-If;
   
   Local string &genAuthCode = String(Int(Rand() * 1000000));;
   
   While Len(&genAuthCode) < 6
      &genAuthCode = String(Int(Rand() * 1000000));
   End-While;
   
   Local number &i;
   Local string &char, &imgBase64, &imgHTML;
   
   For &i = 1 To Len(&genAuthCode)
      &char = Substring(&genAuthCode, &i, 1);
      &imgBase64 = GetHTMLText(@("HTML.UA_CAPTCHA_BASE64_" | &char));
      &imgHTML = &imgHTML | GetHTMLText(HTML.TTLE_CAPTCHA_IMG_WRAPPER, &imgBase64);
   End-For;
   
   &captchaRec.HTMLAREA1.Value = "<table border=0 bgcolor=#C0C0C0><tr><td>" | &imgHTML | "</td></tr></table>";
   
   &captchaRec.AUTH_KEYWORD.Visible = True;
   
   &captchaRec.AUTH_KEYWORD.Value = "";
   &captchaRec.AUTH_KEYWORD.SetCursorPos(%Page);
   
   &captchaRec.REFRESH_ICN.LabelImage = Image.UA_CAPTCHA_REFRESH_D;
   &captchaRec.HTMLAREA2.Value = &captchaRec.HTMLAREA2.Value | Char(10) | GetHTMLText(HTML.UA_CAPTCHA_REFRESH_SCRIPT);
   
   Return &genAuthCode;
   
End-Function;

&captchaCode = buildCaptcha();
